get_filename_component(TWCLIENT_PKGCONFIG_DIR "${CMAKE_CURRENT_LIST_DIR}/../../pkgconfig" ABSOLUTE)
set(ENV{PKG_CONFIG_DIR})
set(ENV{PKG_CONFIG_PATH} ${TWCLIENT_PKGCONFIG_DIR})

set(CHECK_VARS)

#check twclient
find_package(PkgConfig)
pkg_check_modules(pc_twclient REQUIRED QUIET twclient)
find_library(TWCLIENT_LOCATION NAMES twclient HINTS ${pc_twclient_LIBRARY_DIRS})
set(TWCLIENT_INCLUDE_DIRS ${pc_twclient_INCLUDE_DIRS})
set(TWCLIENT_LIBDIRS ${pc_twclient_LIBRARY_DIRS})
set(TWCLIENT_LIBS  ${pc_twclient_LIBRARIES})
list(APPEND CHECK_VARS
  TWCLIENT_LOCATION
  TWCLIENT_INCLUDE_DIRS
  TWCLIENT_LIBDIRS
  TWCLIENT_LIBS)

#check all the modules requested
foreach(COMPO ${twclient_FIND_COMPONENTS})

  pkg_check_modules(pc_twclient_${COMPO} REQUIRED twclient-${COMPO})
  find_library(TWCLIENT_${COMPO}_LOCATION NAMES twclient-${COMPO} HINTS ${pc_twclient_${COMPO}_LIBRARY_DIRS})
  set(TWCLIENT_${COMPO}_INCLUDE_DIRS ${pc_twclient_${COMPO}_INCLUDE_DIRS})
  set(TWCLIENT_${COMPO}_LIBDIRS ${pc_twclient_${COMPO}_LIBRARY_DIRS})
  set(TWCLIENT_${COMPO}_LIBS ${pc_twclient_${COMPO}_LIBRARIES})
  list(APPEND CHECK_VARS
    TWCLIENT_${COMPO}_LOCATION
    TWCLIENT_${COMPO}_INCLUDE_DIRS
    TWCLIENT_${COMPO}_LIBDIRS
    TWCLIENT_${COMPO}_LIBS)

endforeach()

include(FindPackageHandleStandardArgs)
find_package_handle_standard_args(TWCLIENT DEFAULT_MSG ${CHECK_VARS})
mark_as_advanced(${CHECK_VARS})

if(TWCLIENT_FOUND AND NOT TARGET twclient::twclient)
  add_library(twclient::twclient UNKNOWN IMPORTED)
  set_target_properties(twclient::twclient PROPERTIES
    IMPORTED_LINK_INTERFACE_LANGUAGES "C"
    IMPORTED_LOCATION "${TWCLIENT_LOCATION}"
    INTERFACE_LINK_DIRECTORIES "${TWCLIENT_LIBDIRS}"
    INTERFACE_LINK_LIBRARIES "${TWCLIENT_LIBS}"
    INTERFACE_INCLUDE_DIRECTORIES "${TWCLIENT_INCLUDE_DIRS}"
    )

foreach(COMPO ${twclient_FIND_COMPONENTS})

  add_library(twclient::${COMPO} UNKNOWN IMPORTED)
  set_target_properties(twclient::${COMPO} PROPERTIES
    IMPORTED_LINK_INTERFACE_LANGUAGES "C"
    IMPORTED_LOCATION "${TWCLIENT_${COMPO}_LOCATION}"
    INTERFACE_INCLUDE_DIRECTORIES "${TWCLIENT_${COMPO}_INCLUDE_DIRS}"
    INTERFACE_LINK_DIRECTORIES "${TWCLIENT_${COMPO}_LIBDIRS}"
    INTERFACE_LINK_LIBRARIES "${TWCLIENT_${COMPO}_LIBS}")

endforeach()

endif()
